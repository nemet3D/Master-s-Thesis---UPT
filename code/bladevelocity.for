C   compile and link with SLATEC and LAPACK libraries
C   -L /opt/local/lib -lslatec -llapack

      SUBROUTINE BLADEVELOCITY()
C   compute the velocity magnitude (speed) on the blade
C   at grid nodes IGRID=1,NGRID
      IMPLICIT NONE
      INTEGER NMAX
      PARAMETER (NMAX=100)
      INTEGER NGRID,IGRID,IERR
      DOUBLEPRECISION XGRID(NMAX),
     &  BLADESHAPEOLD(NMAX),BLADESLOPEOLD(NMAX),
     &  BLADESHAPENEW(NMAX),BLADESLOPENEW(NMAX),
     &  BLADESEGMENT,VY1,VY2 !,PAR(3)
      DOUBLEPRECISION TWOPI,T,X,S,FX,DFX,FT
      DOUBLEPRECISION LOADING,LOADING1,FVBL
      DOUBLEPRECISION VBL(NMAX), VBLLO(NMAX), VBLUP(NMAX),
     &  VBLleft,VBLright
      DOUBLEPRECISION EPSABS,EPSREL,ABSERR
      INTEGER LIMIT,LENW,NEVAL,LAST
      PARAMETER (EPSABS=1.D-4,EPSREL=1.D-3)
      PARAMETER (LIMIT=200,LENW=4*LIMIT)
      INTEGER IWORK(LIMIT)
      DOUBLEPRECISION WORK(LENW)

      INTEGER NPTS2
      DOUBLEPRECISION POINTS(3)

      COMMON /GRID/ XGRID,BLADESEGMENT,NGRID,IGRID
      COMMON /BLADE/BLADESHAPEOLD,BLADESLOPEOLD,
     &              BLADESHAPENEW,BLADESLOPENEW
C      COMMON /PARAMS/ PAR
      COMMON /FLOWDIR/ VY1,VY2
      COMMON /SPACING/ S ! cascade spacing
      COMMON /VELOCITY/ VBL,VBLLO,VBLUP
      EXTERNAL LOADING,LOADING1
      EXTERNAL FVBL
      TWOPI=6.283185307179586D0
      NPTS2=3 ! two more than the user-supplied brek points
      DO IGRID=2,NGRID-1
C   use QAGP quadrature subroutine
C   user-supplied break points (with possible difficulties)
        POINTS(1)=XGRID(IGRID)
        CALL DQAGP(FVBL,XGRID(1),XGRID(NGRID),NPTS2,POINTS,
     &  EPSABS,EPSREL,VBL(IGRID),ABSERR,NEVAL,IERR,
     &  LIMIT,LENW,LAST,IWORK,WORK)
        VBL(IGRID)=VBL(IGRID)+1.D0+BLADESLOPENEW(IGRID)*
     &  (VY1+(VY2-VY1)*LOADING1(XGRID(IGRID))) ! BLADESLOPE initial
        VBL(IGRID)=VBL(IGRID)/DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
        VBLLO(IGRID)=VBL(IGRID)+0.5D0*S*(VY2-VY1)*LOADING(XGRID(IGRID))
     &  /DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
        VBLUP(IGRID)=VBL(IGRID)-0.5D0*S*(VY2-VY1)*LOADING(XGRID(IGRID))
     &  /DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
      END DO

      IGRID=1
      CALL DGAUS8(FVBL,XGRID(1),XGRID(NGRID),1.D-10,VBL(IGRID),IERR)
      VBL(IGRID)=VBL(IGRID)+1.D0+BLADESLOPENEW(IGRID)*
     &  (VY1+(VY2-VY1)*LOADING1(XGRID(IGRID))) ! BLADESLOPE initial
      VBL(IGRID)=VBL(IGRID)/DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
      VBLLO(IGRID)=VBL(IGRID)+0.5D0*S*(VY2-VY1)*LOADING(XGRID(IGRID))
     &  /DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
      VBLUP(IGRID)=VBL(IGRID)-0.5D0*S*(VY2-VY1)*LOADING(XGRID(IGRID))
     &  /DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)

      IGRID=NGRID
      CALL DGAUS8(FVBL,XGRID(1),XGRID(NGRID),1.D-10,VBL(IGRID),IERR)
      VBL(IGRID)=VBL(IGRID)+1.D0+BLADESLOPENEW(IGRID)*
     &  (VY1+(VY2-VY1)*LOADING1(XGRID(IGRID))) ! BLADESLOPE initial
      VBL(IGRID)=VBL(IGRID)/DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
      VBLLO(IGRID)=VBL(IGRID)+0.5D0*S*(VY2-VY1)*LOADING(XGRID(IGRID))
     &  /DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)
      VBLUP(IGRID)=VBL(IGRID)-0.5D0*S*(VY2-VY1)*LOADING(XGRID(IGRID))
     &  /DSQRT(1.D0+BLADESLOPENEW(IGRID)**2)



      RETURN
      END SUBROUTINE BLADEVELOCITY

      DOUBLEPRECISION FUNCTION FVBL(T)
C   integrand for updating BLADESHAPE(IGRID)
      IMPLICIT NONE
      INTEGER NMAX
      PARAMETER (NMAX=100)
      INTEGER NGRID,IGRID,IERR
      DOUBLEPRECISION XGRID(NMAX),
     &  BLADESHAPEOLD(NMAX),BLADESLOPEOLD(NMAX),
     &  BLADESHAPENEW(NMAX),BLADESLOPENEW(NMAX),
     &  BLADESEGMENT,VY1,VY2 !,PAR(3)
      DOUBLEPRECISION TWOPI,T,X,S,FX,DFX,FT
      DOUBLEPRECISION LOADING

      INTEGER INCFD,NE
      PARAMETER (INCFD=1,NE=1)
      LOGICAL SKIP
      DOUBLEPRECISION XE(1),FE(1)

      COMMON /GRID/ XGRID,BLADESEGMENT,NGRID,IGRID
      COMMON /BLADE/BLADESHAPEOLD,BLADESLOPEOLD,
     &              BLADESHAPENEW,BLADESLOPENEW
C      COMMON /PARAMS/ PAR
      COMMON /FLOWDIR/ VY1,VY2
      COMMON /SPACING/ S ! cascade spacing
C      EXTERNAL LOADING
      TWOPI=6.283185307179586D0
C   grid point where we update the blade
      X=XGRID(IGRID)
      FX =BLADESHAPEOLD(IGRID)
      DFX=BLADESLOPEOLD(IGRID)
C   hermite interpolation of the function FT
      SKIP=.TRUE.
      XE(1)=T
      CALL DPCHFE(NGRID,XGRID,BLADESHAPEOLD,BLADESLOPEOLD,
     &  INCFD,SKIP,NE,XE,FE,IERR)
      FT=FE(1)
C   compute the integrand for blade average velocity
      FVBL=DFX*DSINH(TWOPI/S*(X-T))-DSIN(TWOPI/S*(FX-FT))
      FVBL=FVBL/(DCOSH(TWOPI/S*(X-T))-DCOS(TWOPI/S*(FX-FT)))
      FVBL=FVBL-DFX*DSIGN(1.D0,X-T)
      FVBL=FVBL*0.5D0*(VY2-VY1)*LOADING(T)
      RETURN
      END FUNCTION FVBL
